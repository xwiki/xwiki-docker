/*
 * See the NOTICE file distributed with this work for additional
 * information regarding copyright ownership.
 *
 * This is free software; you can redistribute it and/or modify it
 * under the terms of the GNU Lesser General Public License as
 * published by the Free Software Foundation; either version 2.1 of
 * the License, or (at your option) any later version.
 *
 * This software is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this software; if not, write to the Free
 * Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
 * 02110-1301 USA, or see the FSF site: http://www.fsf.org.
 */
import org.apache.tools.ant.filters.ReplaceTokens

// Run this Gradle build with 'gradle' to generate the various versions and variants from the template directory.
// Whenever a new version of XWiki is out, update this file to update the token values, run gradle and commit the
// result.
//
// Note: As a consequence only update the template files and never the generated files!

defaultTasks 'generate'

def variants = ['mysql-tomcat', 'mariadb-tomcat', 'postgres-tomcat']

// Notes:
// - To compute the XWiki sha256, download the XWiki WAR from
//   http://nexus.xwiki.org/nexus/content/groups/public/org/xwiki/platform/xwiki-platform-distribution-war, and issue:
//   - Unix: sha256sum <binary name>
//   - Mac: shasum --algorithm 256 <binary name>
// - To compute the mysql JDBC sha256, get the JAR at https://search.maven.org/artifact/com.mysql/mysql-connector-j and
//   do the same as for the XWiki sha256.
// - To compute the MariaDB JDBC sha256, get the JAR at
//   https://search.maven.org/artifact/org.mariadb.jdbc/mariadb-java-client and  do the same as for the XWiki sha256.
// - To compute the Postgres JDBC sha256, get the JAR at
//   https://search.maven.org/artifact/org.postgresql/postgresql and do the same as for the XWiki sha256.
// - Use the JDBC driver versions found in the XWiki POM for the specified versions:
//   - mysql: https://github.com/xwiki/xwiki-platform/blob/master/pom.xml#L110 (link for master)
//   - mariadb: https://github.com/xwiki/xwiki-platform/blob/master/pom.xml#L113 (link for master)
//   - postgresql: https://github.com/xwiki/xwiki-platform/blob/master/pom.xml#L119 (link for master)

// Versions of mysql, mariadb and postgres currently in use in the docker images.
def mysql = [
    '9': [
        JDBCVersion: '9.6.0',
        JDBCSha256: '66df1d453789dc8cb759a7dc17f58646893bf28483f262328650f170472a6ead'
    ]
]

def mariadb = [
    '3.5': [
        JDBCVersion: '3.5.7',
        JDBCSha256: '07bb1229dc184f3313a5aef4c5a6b3207c8dbaa09db4a26814c936f004b4c526'
    ]
]

def postgres = [
    '42': [
        JDBCVersion: '42.7.9',
        JDBCSha256: '88f1fc3992e80ec3b048f798030e9a014aa4783c40afb56d3e7a87ee0adf166f'
    ]
]

def tokens = [
    '18': [
        xwikiVersion: '18.0.1',
        xwikiSha256: '1c37eebab186bb2e8441d8ef2dc7b20e2da53f9767627205a88fd2cdb50361e1',
        mysqlJDBCVersion: mysql['9'].JDBCVersion,
        mysqlJDBCSha256: mysql['9'].JDBCSha256,
        mariadbJDBCVersion: mariadb['3.5'].JDBCVersion,
        mariadbJDBCSha256: mariadb['3.5'].JDBCSha256,
        postgresJDBCVersion: postgres['42'].JDBCVersion,
        postgresJDBCSha256: postgres['42'].JDBCSha256,
        tomcatImageTag: [
            tomcatVersion: '10',
            javaVersion: '21'
        ]
    ],
    '17': [
        xwikiVersion: '17.10.3',
        xwikiSha256: '933b524e132bff6ab02bf801a85ac58f676795ab8c1fb3b48fa86023f7cf96fd',
        mysqlJDBCVersion: mysql['9'].JDBCVersion,
        mysqlJDBCSha256: mysql['9'].JDBCSha256,
        mariadbJDBCVersion: mariadb['3.5'].JDBCVersion,
        mariadbJDBCSha256: mariadb['3.5'].JDBCSha256,
        postgresJDBCVersion: postgres['42'].JDBCVersion,
        postgresJDBCSha256: postgres['42'].JDBCSha256,
        tomcatImageTag: [
            tomcatVersion: '10',
            javaVersion: '21'
        ]
    ],
    '17.4': [
        xwikiVersion: '17.4.9',
        xwikiSha256: '552133d573869c8316f8ad70f540d5e117a1802d96666a6e3dc7474c39ffa0b2',
        mysqlJDBCVersion: mysql['9'].JDBCVersion,
        mysqlJDBCSha256: mysql['9'].JDBCSha256,
        mariadbJDBCVersion: mariadb['3.5'].JDBCVersion,
        mariadbJDBCSha256: mariadb['3.5'].JDBCSha256,
        postgresJDBCVersion: postgres['42'].JDBCVersion,
        postgresJDBCSha256: postgres['42'].JDBCSha256,
        tomcatImageTag: [
            tomcatVersion: '10',
            javaVersion: '21'
        ]
    ],
    '16': [
        xwikiVersion: '16.10.16',
        xwikiSha256: 'e5df973b0fda8701e15bde16d5c2786a9cb2c5a1dde719bc4abe5e78c757a3f0',
        mysqlJDBCVersion: mysql['9'].JDBCVersion,
        mysqlJDBCSha256: mysql['9'].JDBCSha256,
        mariadbJDBCVersion: mariadb['3.5'].JDBCVersion,
        mariadbJDBCSha256: mariadb['3.5'].JDBCSha256,
        postgresJDBCVersion: postgres['42'].JDBCVersion,
        postgresJDBCSha256: postgres['42'].JDBCSha256,
        tomcatImageTag: [
            tomcatVersion: '9',
            javaVersion: '21'
        ]
    ]
]

task generate() {
    doLast {
        // Copy the template for all versions and variants
        tokens.keySet().each() { version ->
            variants.each() { variant ->
                // Extract the db type and add it as a token
                def (db, servlet) = variant.tokenize('-')
                tokens[version].'db' = db
                // Copy common template files, evaluating groovy in them
                copy {
                    from 'template'
                    into "${version}/${variant}"
                    include '.env'
                    include 'Dockerfile'
                    include 'docker-compose.yml'
                    include 'xwiki/*'
                    expand(tokens[version])
                    filteringCharset = 'UTF-8'
                }
                // Copy DB-specific template files, evaluating groovy in them
                copy {
                    from 'template'
                    into "${version}/${variant}"
                    include "${db}/*"
                    expand(tokens[version])
                    filteringCharset = 'UTF-8'
                }
                // Copy Servlet-specific template files, evaluating groovy in them
                copy {
                    from 'template'
                    into "${version}/${variant}"
                    include "${servlet}/*"
                    expand(tokens[version])
                    filteringCharset = 'UTF-8'
                }
            }
        }
    }
}
